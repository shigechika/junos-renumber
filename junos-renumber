#!/usr/bin/awk -f
# junos-renumber - Juniper MX's stateful-firewall and SRX's policy rules renumbering
# - Input display set sytle config.
#   show configuretion | display set
# - Automatic detecting MX or SRX
# - Default line count up 10 unit.
#   If you want another unit, run with -v "unit=1" option
# - Good luck :-)
BEGIN {
    num =0
    reset = 1
    if (unit == "") {
	unit = 10
    }
}

# MX
# before
# set services stateful-firewall rule Untrust term Untrust-Trust-1 from ...
# set services stateful-firewall rule Untrust term Untrust-Trust-2 from ...
# set services stateful-firewall rule Untrust term Untrust-Trust-3 from ...
# 1   2        3                 4    5       6    7               8    9    10
# after
# rename services stateful-firewall rule Untrust term Untrust-Trust-1 to term Untrust-Trust-010
# rename services stateful-firewall rule Untrust term Untrust-Trust-2 to term Untrust-Trust-020
# rename services stateful-firewall rule Untrust term Untrust-Trust-3 to term Untrust-Trust-030
# 1      2        3                 4    5       6    7               8  9    10
/set services stateful-firewall rule .* term/ {
    if ( reset ) {
	prefix = $7
	sub("-[0-9]+$", "", prefix)
	regexp = prefix"-[0-9]+$"
#	print "PREFIX:" prefix
#	print "REGEXP:" regexp
	reset = 0
    }
    if ($7 ~ regexp) {
	if (last != $7) {
	    num = num + unit
	    last = $7
	    name = sprintf( "%s-%03d", prefix, num )
	    if (last != name) {
		print "rename "$2" "$3" "$4" "$5" "$6" "$7" to term "name
	    }

	}
    } else {
	reset = 1
	num = 0
    }
}

# SRX
# before
# set security policies from-zone Untrust to-zone Trust policy Untrust-Trust-1 match ...
# set security policies from-zone Untrust to-zone Trust policy Untrust-Trust-2 match ...
# set security policies from-zone Untrust to-zone Trust policy Untrust-Trust-3 match ...
# 1   2        3        4         5       6       7     8      9               10    11
# after
# rename security policies from-zone Untrust to-zone Trust policy Untrust-Trust-1 to policy Untrust-Trust-010
# rename security policies from-zone Untrust to-zone Trust policy Untrust-Trust-2 to policy Untrust-Trust-020
# rename security policies from-zone Untrust to-zone Trust policy Untrust-Trust-3 to policy Untrust-Trust-030
# 1      2        3        4         5       6       7     8      9               10 11     12
/set security policies from-zone .* to-zone .* policy/ {
    if ( reset ) {
	prefix = $9
	sub("-[0-9]+$", "", prefix)
	regexp = prefix"-[0-9]+$"
#	print "PREFIX:" prefix
#	print "REGEXP:" regexp
	reset = 0
    }
    if ($9 ~ regexp) {
	if (last != $9) {
	    num = num + unit
	    last = $9
	    name = sprintf( "%s-%03d", prefix, num )
	    if (last != name) {
		print "rename "$2" "$3" "$4" "$5" "$6" "$7" "$8" "$9" " to policy "" name
	    }
	}
    } else {
	reset = 1
	num = 0
    }
}
